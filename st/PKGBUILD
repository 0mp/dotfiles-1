# Maintainer: Jose Riha <jose1711 gmail com>
# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.2
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'libxext')
makedepends=('ncurses')
url="https://st.suckless.org"
source=(https://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
        https://st.suckless.org/patches/bold-is-not-bright/st-bold-is-not-bright-20190127-3be4cf1.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-$pkgver.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-mouse-$pkgver.diff
        https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-0.8.diff
        usercflags.diff
        config.h
        )
sha256sums=('aeb74e10aa11ed364e1bcc635a81a523119093e63befd2f231f8b0705b15bf35'
            '329169acac7ceaf901995d6e0897913089b799d8cd150c7f04c902f4a5b8eab2'
            '9c5aedce2ff191437bdb78aa70894c3c91a47e1be48465286f42d046677fd166'
            '6103a650f62b5d07672eee9e01e3f4062525083da6ba063e139ca7d9fd58a1ba'
            'bcfc106089d9eb75aa014d4915ed3e6842f1df54edd8b75597154096333df6fa'
            'SKIP'
            'SKIP'
            )

prepare() {
  # user is supposed to maintain config.h him/herself
  cp $srcdir/config.h $srcdir/$pkgname-$pkgver/config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-bold-is-not-bright-20190127-3be4cf1.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-scrollback-$pkgver.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-scrollback-mouse-$pkgver.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/st-scrollback-mouse-altscreen-0.8.diff
  patch -p1 -d $srcdir/$pkgname-$pkgver < $srcdir/usercflags.diff
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 \
      USERCFLAGS=-DUSERFONT="\"\\\"xos4 Terminus:pixelsize=${FONTSIZE:-16}:antialias=true:autohint=true\\\"\""
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  # remove to avoid conflict with ncurses
  rm "${pkgdir}/usr/share/terminfo/s/st" "${pkgdir}/usr/share/terminfo/s/st-256color"
}
